module ModelProperties where

open import Level using (0â„“)
open import Function using (_âˆ˜_) renaming (id to idâ„“)

open import Data.Product

open import Relation.Binary.PropositionalEquality

id = idâ„“ {0â„“}

module ISet
  -- worlds
  (W : Set)
  -- accessibility relation
  (_â‰¤_ : W â†’ W â†’ Set)
  -- relation is a preorder
  (refl-â‰¤  : {w : W} â†’ w â‰¤ w)
  (trans-â‰¤ : {w w' w'' : W} â†’ w â‰¤ w' â†’ w' â‰¤ w'' â†’ w â‰¤ w'')
  -- initial world
  (w0 : W)
  (init : {w : W} â†’ w0 â‰¤ w)
  where

  infixr 19 _â‡’áµ¢_
  infixr 19 _â‡’â‚š_
  infixr 18 _â†’Ì‡áµ¢_
  infixr 18 _â†’Ì‡â‚š_
  
  -- a family of sets (called TYPE in the Coq impl.)
  ISet : Setâ‚
  ISet = (w : W) â†’ Set

  -- a "raw" presheaf (i.e., without the laws)
  Psh : Setâ‚
  Psh = Î£ ISet Î» A â†’ âˆ€ {w w' : W} â†’ w â‰¤ w' â†’ A w â†’ A w'

  -- monotonicity of objects in Psh
  mon : (P : Psh) â†’ âˆ€ {w w' : W} â†’ w â‰¤ w' â†’ projâ‚ P w â†’ projâ‚ P w'
  mon = projâ‚‚

  private
    variable 
      A B C : ISet
      X Y Z : Set
      P Q R : Psh

  -- morphisms in ISet
  _â†’Ì‡áµ¢_ : ISet â†’ ISet â†’ Set
  A â†’Ì‡áµ¢ B = âˆ€ {w : W} â†’ A w â†’ B w

  -- exponentials in ISet
  _â‡’áµ¢_ : ISet â†’ ISet â†’ ISet
  A â‡’áµ¢ B = Î» w â†’ A w â†’ B w

  -- products in ISet
  _Ã—áµ¢_ : ISet â†’ ISet â†’ ISet
  A Ã—áµ¢ B = Î» w â†’ A w Ã— B w

  -- morphisms in Psh
  _â†’Ì‡â‚š_ : Psh â†’ Psh â†’ Set
  P â†’Ì‡â‚š Q = projâ‚ P â†’Ì‡áµ¢ projâ‚ Q

  -- products in Psh
  _Ã—â‚š_ : Psh â†’ Psh â†’ Psh
  P Ã—â‚š Q = projâ‚ P Ã—áµ¢ projâ‚ Q , Î» { wRw' (p , q)  â†’ mon P wRw' p , mon Q wRw' q }
  
  --
  -- Functors of interest
  --
  
  ğ’° : Psh â†’ ISet
  ğ’° = projâ‚
  
  ğ’°â‚ : (f : P â†’Ì‡â‚š Q) â†’ ğ’° P â†’Ì‡áµ¢ ğ’° Q
  ğ’°â‚ f = f

  â„± : ISet â†’ Psh
  â„± A = (Î» w â†’ Î£ W (Î» w' â†’ w' â‰¤ w Ã— A w')) , Î» { wRw' (v , vRw , Av) â†’ v , (trans-â‰¤ vRw wRw' , Av) }

  â„±áµ¢ : (f : A â†’Ì‡áµ¢ B) â†’ â„± A â†’Ì‡â‚š â„± B
  â„±áµ¢ f (w , wRw' , a)= w , wRw' , f a
  
  ğ’ : ISet â†’ Psh
  ğ’ A = (Î» w â†’ âˆ€ {w' : W} â†’ w â‰¤ w' â†’ A w') , Î» wRw' f w'Rw'' â†’ f (trans-â‰¤ wRw' w'Rw'')

  ğ’â‚ : (f : A â†’Ì‡áµ¢ B) â†’ ğ’ A â†’Ì‡â‚š ğ’ B
  ğ’â‚ f g wRw' = f (g wRw')

  -- known: â„± âŠ£ ğ’° âŠ£ ğ’

  Con : Set â†’ ISet
  Con X = Î» _ â†’ X

  Conâ‚ : (f : X â†’ Y) â†’ Con X â†’Ì‡áµ¢ Con Y
  Conâ‚ f {_} = f
  
  Pi : ISet â†’ Set
  Pi X = âˆ€ {w : W} â†’ X w

  Piâ‚ : (f : A â†’Ì‡áµ¢ B) â†’ Pi A â†’ Pi B
  Piâ‚ f a = f a

  Si : ISet â†’ Set
  Si X = Î£ W X

  Siáµ¢ : (f : A â†’Ì‡áµ¢ B) â†’ Si A â†’ Si B
  Siáµ¢ f (w , a) = w , (f a)

  -- known: Si âŠ£ Con âŠ£ Pi 

  GS : Psh â†’ Set
  GS (A , _) = A w0

  GSâ‚ : (P â†’Ì‡â‚š Q) â†’ GS P â†’ GS Q
  GSâ‚ f = f

  -- known GSâ‚ âˆ˜ ğ’ â‰… Pi (when w0 is initial)
  
  --
  -- A comonad â—» on ISet
  --
  
  â—»_ : ISet â†’ ISet
  â—»_ = ğ’° âˆ˜ ğ’
  
  â—»â‚_ : (f : A â†’Ì‡áµ¢ B) â†’ â—» A â†’Ì‡áµ¢ â—» B
  â—»â‚_ {A} {B} = ğ’°â‚ {ğ’ A} {ğ’ B} âˆ˜ ğ’â‚
  
  Îµâ—» : â—» A â†’Ì‡áµ¢ A
  Îµâ—» f = f refl-â‰¤

  Î½â—» : â—» A â†’Ì‡áµ¢ â—» â—» A
  Î½â—» f wRw' w'Rw'' = f (trans-â‰¤ wRw' w'Rw'')

  --
  -- A comonad â—¼ on ISet
  --
  
  â—¼_ : ISet â†’ ISet
  â—¼_ = Con âˆ˜ Pi

  Îµâ—¼ : â—¼ A â†’Ì‡áµ¢ A
  Îµâ—¼ {w = w} p = p {w}

  Î½â—¼ : â—¼ A â†’Ì‡áµ¢ â—¼ â—¼ A
  Î½â—¼ p {u} {v} = p {v}  

  --
  -- A monad â„³ on Set
  --

  â„³ : Set â†’ Set
  â„³ = Pi âˆ˜ â—»_ âˆ˜ Con

  â„³â‚ : (X â†’ Y) â†’ â„³ X â†’ â„³ Y
  â„³â‚ = Piâ‚ âˆ˜ â—»â‚_ âˆ˜ Conâ‚
  
  Î·â„³ : X â†’ â„³ X
  Î·â„³ x _wRw' = x

  Î¼â„³ : â„³ (â„³ X) â†’ â„³ X
  Î¼â„³ ddx wRw' = ddx wRw' wRw'

  Î¼â„³-assoc : Î¼â„³ âˆ˜ â„³â‚ Î¼â„³ â‰¡ Î¼â„³ âˆ˜ Î¼â„³ {â„³ X}
  Î¼â„³-assoc = refl

  Î·â„³-unit-left : Î¼â„³ âˆ˜ Î·â„³ â‰¡ id {â„³ X}
  Î·â„³-unit-left = refl

  Î·â„³-unit-right : Î¼â„³ âˆ˜ â„³â‚ Î·â„³ â‰¡ id {â„³ X}
  Î·â„³-unit-right = refl
  
  --
  -- Necessitation and denecessitation
  --
  
  denec : Pi (â—» A) â†’ Pi A
  denec = Piâ‚ Îµâ—»

  nec : Pi A â†’ Pi (â—» A)
  nec = Piâ‚ (â—»â‚ Îµâ—¼) âˆ˜ Î·â„³

  -- TODO: derive this equality from
  -- properties of nec and denec
  denecâˆ˜nec-is-id : denec âˆ˜ nec â‰¡ id {Pi A}
  denecâˆ˜nec-is-id = refl
 
  --
  -- Corollaries
  --
  
  _â‡’â‚š_ : Psh â†’ Psh â†’ Psh
  P â‡’â‚š Q = (â—» (ğ’° P â‡’áµ¢ ğ’° Q)) , Î» wRw' f w'Rw'' pAtw'' â†’ f (trans-â‰¤ wRw' w'Rw'') pAtw''
  
  fromâ‡’â‚š : Pi (ğ’° (P â‡’â‚š Q)) â†’ Pi (ğ’° P â‡’áµ¢ ğ’° Q)
  fromâ‡’â‚š = denec
  
  toâ‡’â‚š : Pi (ğ’° P â‡’áµ¢ ğ’° Q) â†’ Pi (ğ’° (P â‡’â‚š Q))
  toâ‡’â‚š = nec

  _ : fromâ‡’â‚š {P} {Q} âˆ˜ toâ‡’â‚š {P} {Q} â‰¡ id
  _ = denecâˆ˜nec-is-id


  
